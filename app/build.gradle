// app
plugins {
    id 'com.android.application'
}

android {
    compileSdk rootProject.ext.ANDROID_COMPILE_SDK

    defaultConfig {
        applicationId "de.k3b.android.fdroid.universalcatalog"
        // api 18 android 4.2.1 requirerd for rounded icons
        minSdk rootProject.ext.ANDROID_MIN_SDK
        targetSdk rootProject.ext.ANDROID_TARGET_SDK
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // because app contains more than 65535 methods
        // Required when setting minSdkVersion to 20 or lower
        multiDexEnabled true

        // https://stackoverflow.com/questions/44322178/room-schema-export-directory-is-not-provided-to-the-annotation-processor-so-we/519334#519334
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        packagingOptions {
            exclude 'META-INF/DEPENDENCIES'
            exclude("META-INF/spring.*")
            merge 'META-INF/license.txt'
            merge 'META-INF/notice.txt'
            /*
            exclude 'META-INF/LICENSE'
            exclude 'META-INF/LICENSE.txt'
            exclude 'META-INF/license.txt'
            exclude 'META-INF/NOTICE'
            exclude 'META-INF/NOTICE.txt'
            exclude 'META-INF/notice.txt'
            exclude 'META-INF/ASL2.0'
            exclude("META-INF/*.kotlin_module")
             */
        }
    }

    buildTypes {
        release {
            shrinkResources true

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
        /* Uncomment to se what progruard will remove
        debug {
            shrinkResources true

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
         */
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        if (rootProject.ext.USE_API_34) coreLibraryDesugaringEnabled true

        sourceCompatibility rootProject.ext.COMPILER_VERSION_COMMON
        targetCompatibility rootProject.ext.COMPILER_VERSION_COMMON
    }


    buildFeatures {
        viewBinding = true
    }
    namespace 'de.k3b.fdroid.android'
    lint {
        disable 'IconLocation' // 'density-independent launcher icon'
    }
}

dependencies {
    implementation(project(path: ':app:fdroid-domain'))
    implementation(project(path: ':app:fdroid-v1'))
    implementation(project(path: ':app:fdroid-html'))
    implementation(project(path: ':app:fdroid-repository-android'))
    testImplementation(project(path: ':app:fdroid-v1'))

    // because app contains more than 65535 methods
    implementation 'androidx.multidex:multidex:2.0.1'

    if (rootProject.ext.USE_API_34) {
        // For AGP 7.4+
        coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
        // For AGP 7.3
        // coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.3'
        // For AGP 4.0 to 7.2
        // coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.9'
    }

    {
        // os specific annotations
        implementation 'io.swagger.core.v3:swagger-annotations:2.2.18'
        implementation "com.fasterxml.jackson.core:jackson-annotations:$rootProject.ext.JACKSON_ANNOTATIONS_VERSION"
        implementation 'javax.persistence:javax.persistence-api:2.2'
        implementation 'androidx.appcompat:appcompat:1.4.1'

        implementation "androidx.room:room-runtime:$rootProject.ext.ROOM_VERSION"
        annotationProcessor "androidx.room:room-compiler:$rootProject.ext.ROOM_VERSION"
    }

    // android specific
    implementation "androidx.work:work-runtime:$rootProject.ext.WORK_VERSION"

    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation("androidx.lifecycle:lifecycle-viewmodel:2.4.1")
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    implementation "com.google.android.material:material:$rootProject.ext.MATERIAL_VERSION"

    implementation "com.google.code.gson:gson:$rootProject.ext.GSON_VERSION"
    implementation "commons-codec:commons-codec:$rootProject.ext.COMMONS_CODEC_VERSION"
    // Hex.en/decodeHex
    implementation "commons-io:commons-io:$rootProject.ext.COMMONS_IO_VERSION"

    // https://github.com/samskivert/jmustache to generate html
    implementation 'com.samskivert:jmustache:1.15'

    implementation "org.slf4j:slf4j-api:$rootProject.ext.SLF4J_VERSION"
    implementation "org.slf4j:slf4j-android:$rootProject.ext.SLF4J_VERSION"
    // implementation "uk.uuid.slf4j:slf4j-android:(rootProject.ext.SLF4J_VERSION)-0"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'

    androidTestImplementation "androidx.work:work-testing:$rootProject.ext.WORK_VERSION"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'

    //     androidTestImplementation 'androidx.test.ext:junit:1.1.5'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

}